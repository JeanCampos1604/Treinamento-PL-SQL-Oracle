
--CURSOR SIMPLES
DECLARE
   VCOD_ALUNO TALUNO2.COD_ALUNO%TYPE;
   VNOME   TALUNO2.NOME%TYPE;
   CURSOR C1 IS
     SELECT COD_ALUNO, NOME
     FROM   TALUNO2;
BEGIN
   OPEN C1; -- ABRE CURSOR
   LOOP
      FETCH C1 INTO VCOD_ALUNO, VNOME;  --PEGA REGISTRO A REGISTRO E ATRIBUI AS RESPECTIVAS VARIAVEIS
      EXIT WHEN C1%ROWCOUNT >= 10 OR C1%NOTFOUND;
      DBMS_OUTPUT.PUT_LINE('Codigo: '||
        LPAD(VCOD_ALUNO,4,'0')||' - '||'Nome: '||VNOME);
   END LOOP;
   CLOSE C1; --FECHA CURSOR
END;


--CURSOR + RECORD
DECLARE
   CURSOR C1 IS
      SELECT * FROM TALUNO2;
   REG C1%ROWTYPE;  --RECORD
BEGIN
   OPEN C1;--ABRE CURSOR
   LOOP
      FETCH C1 INTO REG;
      EXIT WHEN C1%ROWCOUNT >= 10 OR C1%NOTFOUND;
      DBMS_OUTPUT.PUT_LINE('Codigo: '||
                  LPAD(REG.COD_ALUNO,5,'0')||'-'||
                 'Nome: '||REG.NOME);
   END LOOP;
   CLOSE C1; --FECHA CURSOR
END;


--CURSOR + RECORD + FOR
DECLARE
  CURSOR C1 IS
    SELECT * FROM TALUNO2;
  REG TALUNO2%ROWTYPE;
BEGIN
  FOR REG IN C1  --EQUIVALE A: OPEN CURSOR, LAÇO, FETCH, CLOSE CURSOR, EXIT WHEN
  LOOP
    DBMS_OUTPUT.PUT_LINE('Codigo: '||
       LPAD(REG.COD_ALUNO,5,'0')||' - ' || 'Nome: '||REG.NOME);
  END LOOP;
END;


--SELECT DIRETO + RECORD + FOR (PARA COMANDOS DE QUERY CURTOS)
DECLARE
  REG TALUNO%ROWTYPE;
BEGIN
  FOR REG IN (SELECT * FROM TALUNO)
  LOOP
    DBMS_OUTPUT.PUT_LINE(REG.COD_ALUNO ||' - ' || REG.NOME);
  END LOOP;
END;


--CURSOR COM PARAMETROS - SEM LAÇO COM APENAS 1 RESULTADO
DECLARE
  CURSOR C1 (PCOD_ALUNO NUMBER) IS
    SELECT * FROM TALUNO2
    WHERE COD_ALUNO = PCOD_ALUNO
   FOR UPDATE OF NOME NOWAIT; --BLOQUEIA A COLUNA NOME PARA ALTERACAO
  
  REG C1%ROWTYPE;
BEGIN
  OPEN C1(&CODIGO);
  FETCH C1 INTO REG;
  DBMS_OUTPUT.PUT_LINE(REG.COD_ALUNO ||' - ' || REG.NOME || ' - ' || REG.CIDADE);
  CLOSE C1; --LIBERA O REGISTRO PARA ALTERACAO
END;



DECLARE
   CURSOR C1 IS
     SELECT * FROM TALUNO2
     FOR UPDATE;
   REG_ALUNO C1%ROWTYPE;
BEGIN
   FOR REG_ALUNO IN C1
   LOOP
      UPDATE TALUNO2
      SET    NOME = INITCAP(REG_ALUNO.NOME)
      WHERE CURRENT OF C1;  --BLOQUEIA SOMENTE O REG ATUAL
      DBMS_OUTPUT.PUT_LINE('Nome: '||INITCAP(REG_ALUNO.NOME));
   END LOOP;
   COMMIT;
END;

SELECT *
  FROM TALUNO2;